(ns minusthree.engine.thorbox.thorbox
  (:require
   [clojure.java.io :as io]
   [clojure.spec.alpha :as s]
   [minusthree.engine.ffm.arena :as arena]
   [minusthree.engine.ffm.memory :as memory]
   [minusthree.engine.sharedlibs :as sharedlibs]
   [minusthree.engine.sugar :refer [ub]]
   [minusthree.engine.utils :as utils :refer [raw-from-here]]
   [minusthree.engine.world :as world]
   [minusthree.gl.cljgl :as cljgl]
   [minusthree.gl.gl-magic :as gl-magic]
   [minusthree.gl.shader :as shader]
   [minusthree.gl.texture :as texture]
   [minusthree.stage.debug-ui :as debug-ui]
   [odoyle.rules :as o])
  (:import
   [java.lang.foreign Arena MemoryLayout]
   [org.lwjgl.opengl GL45]
   [thorvg tvg]))

(defonce _loadlib
  (try
    (sharedlibs/load-libs "libthorvg-1")
    (sharedlibs/load-libs "box2dd")
    (catch Throwable err
      (println "loading lib err" (:cause (Throwable->map err))))))

(s/def ::tvg-arena ::arena/arena)
(s/def ::buffer|| ::memory/segment)
(s/def ::canvas|| ::memory/segment)
(s/def ::text|| ::memory/segment)

(s/def ::texture some?)
(s/def ::width number?)
(s/def ::height number?)
(s/def ::vao ::gl-magic/vao)
(s/def ::program-info ::shader/program-info)
(s/def ::render-data
  (s/keys :req [::program-info ::vao ::texture
                ::width ::height]))

(def plane3d-vertices
  (float-array
   [-1.0 -1.0 0.0  1.0 -1.0 0.0 -1.0  1.0 0.0
    -1.0  1.0 0.0  1.0 -1.0 0.0  1.0  1.0 0.0]))

(def plane3d-uvs
  (float-array
   [0.0 1.0  1.0 1.0  0.0 0.0
    0.0 0.0  1.0 1.0  1.0 0.0]))

(def fbo-vs (raw-from-here "mainscreen.vert"))
(def fbo-fs (raw-from-here "mainscreen.frag"))

(defn create-thor-gl []
  (let [program-info (cljgl/create-program-info-from-source fbo-vs fbo-fs)
        gl-summons     (gl-magic/cast-spell
                        [{:bind-vao "thor"}
                         {:buffer-data plane3d-vertices :buffer-type GL45/GL_ARRAY_BUFFER}
                         {:point-attr :a_pos :use-shader program-info :count 3 :component-type GL45/GL_FLOAT}
                         {:buffer-data plane3d-uvs :buffer-type GL45/GL_ARRAY_BUFFER}
                         {:point-attr :a_uv :use-shader program-info :count 2 :component-type GL45/GL_FLOAT}
                         {:unbind-vao true}])
        vao          (-> gl-summons ::gl-magic/data ::gl-magic/vao (get "thor"))
        id->buffer   (-> gl-summons ::gl-magic/data ::shader/buffer)]
    {::program-info program-info ::vao vao ::id->buffer id->buffer}))

(declare stuff-to-draw-to)

(defn after-refresh
  [new-world {config :config}]
  (try
    (tvg/tvg_engine_init 4)
    (let [tvg-arena (Arena/ofAuto)
          window   (-> config :window-conf)
          width    (or (:w window) 400)
          height   (or (:h window) 400)
          buffer|| (.allocate tvg-arena (MemoryLayout/sequenceLayout (* width height) tvg/C_INT))
          canvas|| (doto (tvg/tvg_swcanvas_create (tvg/TVG_ENGINE_OPTION_DEFAULT))
                     (tvg/tvg_swcanvas_set_target buffer|| width width height (tvg/TVG_COLORSPACE_ABGR8888S)))
          texture  (texture/create-texture (.asByteBuffer buffer||) width height)
          gl-data  (assoc (create-thor-gl)
                          ::texture texture
                          ::width width
                          ::height height)]
      (println "create thorbox context")
      (tvg/tvg_font_load (.allocateFrom tvg-arena (str (io/file (io/resource "public/fonts/CardboardCrown.ttf")))))

      (let [drawn-canvas (stuff-to-draw-to tvg-arena canvas||)]
        (o/insert new-world
                  ::tvg (merge
                         drawn-canvas
                         {::tvg-arena tvg-arena
                          ::buffer|| buffer||
                          ::render-data gl-data}))))
    (catch Throwable _err
      (println "thorbox no-op init")
      new-world)))

(defn stuff-to-draw-to [arena canvas||]
  (let [rect||  (doto (tvg/tvg_shape_new)
                  (tvg/tvg_shape_append_rect #_x-y 100 100 #_w-h 100 100 #_rx-ry 15 15 #_cw? false)
                  (tvg/tvg_shape_set_fill_color #_rgba (ub 255) (ub 220) (ub 255) (ub 255))
                  (tvg/tvg_shape_set_stroke_width 4)
                  (tvg/tvg_shape_set_stroke_color (ub 13) (ub 10) (ub 10) (ub 255)))
        rect2|| (doto (tvg/tvg_shape_new)
                  (tvg/tvg_shape_append_rect #_x-y 120 120 #_w-h 100 100 #_rx-ry 15 15 #_cw? false)
                  (tvg/tvg_shape_set_fill_color #_rgba (ub 255) (ub 220) (ub 255) (ub 255))
                  (tvg/tvg_shape_set_stroke_width 4)
                  (tvg/tvg_shape_set_stroke_color (ub 13) (ub 10) (ub 10) (ub 255)))
        path||  (doto (tvg/tvg_shape_new)
                  ;; Tvg_Paint paint, float cx1, float cy1, float cx2, float cy2, float x, float y
                  (tvg/tvg_shape_move_to 275, 267)
                  (tvg/tvg_shape_cubic_to 259, 29, 99, 279, 275, 267)
                  (tvg/tvg_shape_cubic_to 537, 245, 300, -170, 274, 430)
                  (tvg/tvg_shape_close)
                  (tvg/tvg_shape_set_stroke_width 4)
                  (tvg/tvg_shape_set_stroke_color (ub 13) (ub 10) (ub 10) (ub 255))
                  (tvg/tvg_shape_set_fill_color #_rgba (ub 90) (ub 180) (ub 180) (ub 255)))
        scene|| (doto (tvg/tvg_scene_new)
                  (tvg/tvg_scene_add rect||)
                  (tvg/tvg_scene_add rect2||))
        text||   (doto (tvg/tvg_text_new)
                   (tvg/tvg_text_set_font (.allocateFrom arena "CardboardCrown"))
                   (tvg/tvg_text_set_size 50)
                   (tvg/tvg_text_set_text (.allocateFrom arena "hello thor from dofida!"))
                   (tvg/tvg_text_set_color (ub 13) (ub 10) (ub 10))
                   (tvg/tvg_paint_translate 150 150))]
    {::canvas|| (doto canvas||
                  (tvg/tvg_canvas_add scene||)
                  (tvg/tvg_canvas_add text||)
                  (tvg/tvg_canvas_add path||))
     ::text|| text||}))

(def rules
  (o/ruleset
   {::thorvg-canvas
    [:what
     [::tvg ::tvg-arena tvg-arena]
     [::tvg ::buffer|| buffer||]
     [::tvg ::canvas|| canvas||]
     [::tvg ::text|| text||]
     [::tvg ::render-data render-data]]}))

;; I haven't been able to use thorvg glcanvas yet
(defn render-buffer-to-gl
  [buffer|| {::keys [program-info vao texture width height]}]
  (GL45/glUseProgram (:program program-info))
  (GL45/glBindVertexArray vao)

  (GL45/glActiveTexture GL45/GL_TEXTURE0)
  (GL45/glBindTexture GL45/GL_TEXTURE_2D texture)
  (GL45/glTexSubImage2D GL45/GL_TEXTURE_2D 0 0 0 width height GL45/GL_RGBA GL45/GL_UNSIGNED_BYTE (.asByteBuffer buffer||))

  (cljgl/set-uniform program-info :u_tex 0)
  (GL45/glDrawArrays GL45/GL_TRIANGLES 0 6))

(defn render [game]
  (let [{:keys [tvg-arena canvas|| text|| buffer|| render-data]}
        (utils/query-one (::world/this game) ::thorvg-canvas)
        {:keys [fps-value position]}
        (utils/query-one (::world/this game) ::debug-ui/fps-panel)]
    #_{:clj-kondo/ignore [:inline-def]}
    (def debug-var (o/query-all (::world/this game) ::thorvg-canvas))
    (when canvas||
      (doto text||
        (tvg/tvg_text_set_text (.allocateFrom tvg-arena (str fps-value)))
        (tvg/tvg_paint_translate (.x position) (.y position)))
      (doto canvas||
        (tvg/tvg_canvas_update)
        (tvg/tvg_canvas_draw #_clear false)
        (tvg/tvg_canvas_sync))
      (render-buffer-to-gl buffer|| render-data)))
  game)

(defn clean-up []
  (try
    (tvg/tvg_engine_term)
    (catch Throwable _err
      (println "thorbox no-op destroy"))))

(defn before-refresh [old-world _old-game]
  (clean-up)
  (-> old-world
      (dissoc ::tvg-arena ::buffer|| ::canvas||)))

(def system
  {::world/after-refresh #'after-refresh
   ::world/rules #'rules
   ::world/before-refresh #'before-refresh})

(comment
  (require '[com.phronemophobic.viscous :as viscous])

  (viscous/inspect debug-var)

  :-)
